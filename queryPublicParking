/*
PUBLIC STREET AND SURFACE PARKING IN BADEN
*/

[out:json][timeout:25];

// Search Area
{{geocodeArea:Baden (Switzerland)}}->.Baden;
(
  {{geocodeArea:Baden (Switzerland)}};
  {{geocodeArea:Wettingen)}};
  )->.BadenWettingen;


// 1) Publicly accessible street parking (operated by city of Baden)
nwr["amenity"="parking"]["access"="yes"]["parking"~"^(street_side|lane|on_kerb|half_on_kerb)$"](area.Baden);
out geom;

make stats streetParking=sum(is_tag("capacity") ? number(t["capacity"]) : 0);
out;

// 2) Surface parkings operated by city of Baden
// Note: Parking Schwimmbad is located in Wettingen

(
  nwr["amenity"="parking"]["access"="yes"]
  ["parking"="surface"]["operator"="Stadt Baden"](area.BadenWettingen);
);
out geom;

make stats surfaceParking=sum(is_tag("capacity") ? number(t["capacity"]) : 0);
out;

// 3.1) Garages ("Parkhäuser") operated by city of Baden
// Garages can be maped with amenity=parking_entrance and/or amenity=parking. To avoid doublecounting, only items with a name are shown.
nwr["amenity"~"^(parking|parking_entrance)$"]["access"="yes"]["name"~"."]["parking"~"^(multi-storey|underground|rooftop)$"]["operator"="Stadt Baden"](area.Baden);

out geom;
make stats garageParkingCity=sum(is_tag("capacity") ? number(t["capacity"]) : 0);
out;

// 3.2) Garages ("Parkhäuser") operated by Parkhaus Ländli AG and Grand Casino Baden AG
// Garages can be maped with amenity=parking_entrance and/or amenity=parking. To avoid doublecounting, only items with a name are shown.
 nwr["amenity"~"^(parking|parking_entrance)$"]["access"="yes"]["name"~"."]["parking"~"^(multi-storey|underground|rooftop)$"]["operator"~"^(Parkhaus Ländli AG|Grand Casino Baden AG)$"](area.Baden);

out geom;
make stats garageParkingLaendliCasino=sum(is_tag("capacity") ? number(t["capacity"]) : 0);
out;


// 4) Public parking (surface or garages) operated by privates
  
    nwr["amenity"~"^(parking|parking_entrance)$"]["access"="yes"]["parking"~"^(surface|multi-storey|underground|rooftop)$"]["operator:type"!="government"]["operator:type"]["operator"!="Parkhaus Ländli AG"]["operator"!="Grand Casino Baden AG"](area.Baden);
out geom;
make stats publicParkingByPrivate=sum(is_tag("capacity") ? number(t["capacity"]) : 0);
out;

// MAP STYLE

{{style:

*[parking=lane]
{ color: steelblue; fill-color:steelblue; }
*[parking=street_side]
{ color: steelblue; fill-color:steelblue; }
*[parking=on_kerb]
{ color: steelblue; fill-color:steelblue; }
*[parking=half_on_kerb]
{ color: steelblue; fill-color:steelblue; }

*[parking=surface]
{ color: steelblue; fill-color:steelblue; }

*[parking=surface][capacity>25]
{ color: #6646b4; fill-color:#6646b4; }

*[parking=multi-storey]
{ color: #b446a7; fill-color:#b446a7 }
*[parking=underground]
{ color: #b446a7; fill-color:#b446a7; }
*[parking=rooftop]
{ color: #b446a7; fill-color:#b446a7; }

*[operator=Parkhaus Ländli AG]
{ color: #b446a7; fill-color:#b446a7; dashes:1,3;}
*[operator=Grand Casino Baden AG]
{ color: #b446a7; fill-color:#b446a7; dashes:1,3;}

*[operator:type][operator!=Stadt Baden][operator!=Parkhaus Ländli AG][operator!=Grand Casino Baden AG]
{ color: red; fill-color:red;}
{text: eval("tag('name')");}

*[capacity>25]
{text: eval("tag('name') . ' (' . tag('capacity') . ')'");}


}}


// https://colorkit.co/color/4682b4/
// https://gist.github.com/govvin/a095afd2fd53c9e5f2af232ef55ffde1
