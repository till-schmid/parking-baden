// @name Private Parking Stadt Baden

/*
PRIVATE PARKING IN BADEN (incomplete!)
*/

[out:json][timeout:25];

// Search Area
{{geocodeArea:Baden (Switzerland)}}->.Baden;
(
  {{geocodeArea:Baden (Switzerland)}};
  {{geocodeArea:Wettingen)}};
  )->.BadenWettingen;


// 1) Private street parking 
nwr["amenity"="parking"]["access"]["access"!="yes"]["parking"~"^(street_side|lane|on_kerb|half_on_kerb)$"](area.Baden);
out geom;

make stats streetParking=sum(is_tag("capacity") ? number(t["capacity"]) : 0);
out;

// 2) Private surface parking 
nwr["amenity"="parking"]["access"]["access"!="yes"]["parking"="surface"](area.Baden);
out geom;

make stats surfaceParking=sum(is_tag("capacity") ? number(t["capacity"]) : 0);
out;

// 3) Private garage parking

nwr["amenity"~"^(parking|parking_entrance)$"]["access"]["access"!="yes"]["parking"~"^(multi-storey|underground|rooftop)$"](area.Baden);

out geom;
make stats garageParking=sum(is_tag("capacity") ? number(t["capacity"]) : 0);
out;

// MAP STYLE

{{style:

*[parking=lane]
{ color: gold; fill-color:gold; }
*[parking=street_side]
{ color: gold; fill-color:gold; }
*[parking=on_kerb]
{ color: gold; fill-color:gold; }
*[parking=half_on_kerb]
{ color: gold; fill-color:gold; }

*[parking=surface]
{ color: goldenrod; fill-color:goldenrod }

*[parking=multi-storey]
{ color: darkorange; fill-color:darkorange }
*[parking=underground]
{ color: darkorange; fill-color:darkorange }
*[parking=rooftop]
{ color: darkorange; fill-color:darkorange }

*[capacity>25]
{text: eval("tag('name') . ' (' . tag('capacity') . ')'");}

}}

// Best use with Tile Server: http://{s}.basemaps.cartocdn.com/light_all/{z}/{x}/{y}.png
